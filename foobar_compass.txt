          IDENT  FOOBAR1,TME
          ABS 
          ENTRY  START
******************************************************************
*         FOOBAR - PRINTER NUMBERS OR FOO OR BAR OR BOTH.        *
*                                                                *
*         IF I IS DIVISIBLE BY 3, PRINT FOO                      *
*         IF I IS DIVISIBLE BY 5, PRINT BAR                      *
*         IF I IS DIVISIBLE BY 3 and 5, PRINT FOOBAR             *
******************************************************************
          SYSCOM B1          TELL COMPILER WE SET B1 TO 1
          ORG    110B
TME       CON    0
*         1      8           30
START     SB1    1           ALWAYS START WITH B1=1
          SX6    B1          LOOP START
          SA6    I           INITIALIZE I TO 1
          BX3    X6          X3 IS I IN OUR LOOP
LOOP      SA4    THREE       X4=3
          RJ     MOD         CALL MOD(I,3): (X3,X4) -> X6
          BX0    X6          COPY OF MOD(I,3) RESULT
          SA4    FIVE        X4=5
          RJ     MOD         CALL MOD(I,5): (X3,X4) -> X6
          BX5    X0+X6       X5 IS MOD(I,3)>0  MOD(I,5)>0
          ZR     X5,MOD3A5   IF NOT (MOD(I,3)>0  MOD(I,5)>0)
          ZR     X6,MOD5
          ZR     X0,MOD3
          EQ     PRINTI
MOD3      WRITEH OUTPUT,FOO,FOOLEN       MOVE TO BUFFER
          EQ     FLUSH
MOD5      WRITEH OUTPUT,BAR,BARLEN       MOVE TO BUFFER
          EQ     FLUSH
MOD3A5    WRITEH OUTPUT,FOOBAR,FBLEN    MOVE TO BUFFER
          EQ     FLUSH
PRINTI    BX1    X3          COPY I TO X1 FOR CDD ROUTINE
          RJ     =XCDD
          SA6    NUM         SAVE RESULT TO MEMORY FOR WRITE  
          WRITEH OUTPUT,NUM,1
*
FLUSH     WRITER OUTPUT,R    FLUSH BUFFER
          SA3    I           X3=I
          SX3    X3+B1       INCREMENT I BY 1
          SA4    MAX         X4=100
          IX4    X4-X3
          BX6    X3          COPY TO REGISTER FOR SAVING TO MEMORY
          NG     X4,LOOPEND  EXIT LOOP IF I GT MAX  
          SA6    I           SAVE I TO MEMORY
          EQ     LOOP        LOOP AGAIN
LOOPEND   WRITEH OUTPUT,DONE,1
          WRITER OUTPUT,R
          ENDRUN
*         CONSTANTS
MAX       DATA   20 
THREE     DATA   3
FIVE      DATA   5
FOO       DATA   3HFOO
FOOLEN    EQU    *-FOO
BAR       DATA   3HBAR
BARLEN    EQU    *-BAR
FOOBAR    DATA   6HFOOBAR
FBLEN     EQU    *-FOOBAR
DONE      DATA   4HDONE
I         BSS    1
NUM       BSS    1
OBUF      BSS    301B
OUTPUT   FILEB     OBUF,301B
******************************************************************
*         SUBROUTINE MOD - CALCULATE MODULUS (REMAINDER)         *
*                                                                *
*         INPUT:  X3,X4                                          *
*         OUTPUT: X6: MOD(X3,X4)                                 *
*         USES:   X6,X7,B7                                       *
*                                                                *
*         INT(N/D) -> X                                          *
*         X*D -> TEMP                                            *
*         N-TEMP -> R                                            *
******************************************************************
MOD       BSS    1           STORAGE FOR OUR RETURN ADDRESS
          BX6    X3          COPY X3 INTO X6
          BX7    X4          COPY X4 INTO X7
*                            X6=X6/X7 (INTEGER)
          PX6                CONVERT TO FLOAT (PACK)
          NX6                NORMALIZE
          PX7                CONVERT TO FLOAT (PACK)
          NX7                NORMALIZE
          FX6    X6/X7       FLOATING POINT X6 = X6/X7
          UX6    B7          CONVERT FLOAT TO INTERGER, EXPONENT IN B7 (UNPACK)
          LX6    B7          LOGICAL SHIFT MANTISSA B7 BITS (REST OF INTEGER CONVERSION)
          IX6    X6*X4       TEMP=X*D
          IX6    X3-X6       R=N-TEMP
          EQ     MOD         RETURN TO CALLER
******************************************************************
*         COMMON DECKS REQUIRED FOR OUR PROGRAM
OPL       XTEXT  COMCSYS
OPL       XTEXT  COMCWTH
OPL       XTEXT  COMCWTO
OPL       XTEXT  COMCCIO
OPL       XTEXT  COMCWTW
OPL       XTEXT  COMCCDD
          END    START
